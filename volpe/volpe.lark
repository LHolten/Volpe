block: _LINE_SEP* (value_ass _LINE_SEP+)* value1 _LINE_SEP*

?value_ass: value1 "!"
    | shape ":=" value1 -> assign
    | shape "+=" value1 -> add_assign
    | shape "-=" value1 -> sub_assign
    | shape "/=" value1 -> div_assign
    | shape "*=" value1 -> mul_assign
    | shape "%=" value1 -> mod_assign
    | shape "**=" value1 -> power_assign
    | value2 "->" value3 -> if_then

?value1: value2

?value2: value3
    | value2 "->" value3 -> implication

?value3: value4
    | value3 "||" value4 -> logic_or

?value4: value5
    | value4 "&&" value5 -> logic_and

?value5: value6
    | value5 "=" value6 -> equals
    | value5 "!=" value6 -> not_equals

?value6: value7
    | value6 "<=" value7 -> less_equals
    | value6 ">=" value7 -> greater_equals
    | value6 "<" value7 -> less
    | value6 ">" value7 -> greater

?value7: value8
    | value7 "+" value8 -> add
    | value7 "-" value8 -> sub

?value8: value9
    | value8 "/" value9 -> div
    | value8 "%" value9 -> mod
    | value8 "*" value9 -> mul
    | value8 "**" value9 -> power

?value9: value10
    | "-" value9 -> negate
    | "!" value9 -> logic_not
    | "~" value9 -> convert_flt
    | value10 "." INTEGER -> convert_int
    | "|" value10 "|" -> list_size

?value10: symbol
    | INTEGER -> integer
    | CHARACTER -> character
    | ESCAPED_CHARACTER -> escaped_character
    | ESCAPED_STRING -> string
    | "(" shapes ")" "{" block "}" -> func
    | "{" _SEP* value1 "," (_SEP* value1 ("," _SEP* value1)*)? _SEP* "}" -> object
    | "{" _SEP* "}" -> object
    | "{" block "}"
    | "(" value1 ")"
    | "[" (_SEP* value1 ("," _SEP* value1)*)? _SEP* "]" -> list
    | value10 "." CNAME -> attribute
    | value10 "(" values ")" -> func_call
    | value10 "[" _SEP* value1 _SEP* "]" -> list_index

values: (_SEP* value1 ("," _SEP* value1)*)? _SEP* -> object
shapes: (_SEP* shape ("," _SEP* shape)*)? _SEP* -> object

?shape: symbol
    | "{" _SEP* shape "," (_SEP* shape ("," _SEP* shape)*)? _SEP* "}" -> object
    | "{" _SEP* "}" -> object
    | shape "[" _SEP* value1 _SEP* "]" -> list_index

symbol: CNAME
    | THIS_FUNC

%import common.ESCAPED_STRING
%import common.INT
%import common.CR
%import common.LF
%import common.CNAME
%ignore WS
WS: /[ \t\f]/+

INTEGER: INT
FLOATING: INT "." INT
ESCAPED_CHARACTER: /'\\[abfnrtv'\\]'/
CHARACTER: /'[^'\\]'/

MULTILINE_COMMENT: "#!" /((?!!#).|\n)*/ "!#" " "*
COMMENT: "#" /.*/
NEWLINE: (CR? LF)

THIS_FUNC: "@"

_LINE_SEP: _SEP
    | ";"

_SEP: MULTILINE_COMMENT NEWLINE
    | COMMENT NEWLINE
    | NEWLINE