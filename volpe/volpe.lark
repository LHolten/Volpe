block: _LINE_SEP* (value (_LINE_SEP+ value)* _LINE_SEP*)?

?value: value1
    | shape "=" value0 -> assign
    | token "+=" value0 -> add_assign
    | token "-=" value0 -> sub_assign
    | token "/=" value0 -> div_assign
    | token "*=" value0 -> mul_assign
    // | token "%=" value0 -> mod_assign
    // | token "**=" value0 -> power_assign

?value0: value1
    | value2 "::" value0 -> map

?value1: value2
    | value2 "->" value0 -> implication
    | "(" (shape ("," shape)*)? ")" "=>" value1 -> func
    | shape "=>" value0 -> func
    | "$" value0 -> func
    | "&" value0 -> make_list

?value2: value3
    | value2 "||" value3 -> logic_or

?value3: value4
    | value3 "&&" value4 -> logic_and

?value4: value5
    | ":>" value0 -> return_n

?value5: value6
    | value5 "==" value6 -> equals
    | value5 "!=" value6 -> not_equals

?value6: value7
    | value6 "<=" value7 -> less_equals
    | value6 ">=" value7 -> greater_equals
    | value6 "<" value7 -> less
    | value6 ">" value7 -> greater

?value7: value8
    | value7 "+" value8 -> add
    | value7 "-" value8 -> sub

?value8: value9
    | value8 "/" value9 -> div
    | value8 "%" value9 -> mod
    | value8 "*" value9 -> mul
    | value9 ".." value9 -> number_iter
    // | value8 "**" value9 -> power

?value9: value10
    | "-" value9 -> negate
    | "!" value9 -> logic_not
    | "~" value9 -> convert_flt
    | value10 "." INTEGER -> convert_int
    | "|" value10 "|" -> list_size

?value10: func
    | "{" block "}"
    | "[" _SEP* (value0 ("," _SEP* value0)* _SEP* )? "]" -> collect_tuple
    | INTEGER -> integer
    // | ESCAPED_STRING -> string

?func: CNAME -> symbol
    | "(" _SEP* value0 _SEP* ")"
    | func "(" _SEP* (value0 ("," _SEP* value0)* _SEP* )? ")" -> func_call
    | func "[" _SEP* value0 _SEP* "]" -> list_index
    | "@" -> this_func

?shape: token
    | "[" _SEP* shape ("," _SEP* shape)* _SEP* "]" -> shape
token: CNAME

%import common.ESCAPED_STRING
%import common.INT
%import common.CR
%import common.LF
%import common.CNAME
%ignore WS
WS: /[ \t\f]/+

INTEGER: INT
FLOATING: INT "." INT

MULTILINE_COMMENT: "#!" /((?!!#).|\n)*/ "!#" " "*
COMMENT: "#" /.*/
NEWLINE: (CR? LF)

_LINE_SEP: _SEP
    | ";"

_SEP: MULTILINE_COMMENT NEWLINE
    | COMMENT NEWLINE
    | NEWLINE