?block: _LINE* super

?super: ".." partial _LINE*
    | term partial (_LINE+ super | _LINE*) -> assign

?expr: term
    | expr symbol -> attribute
    | expr arg -> func_call
    | operator term -> func_call

?operator: expr BOOL_TYPE -> bool
    | expr COMP_TYPE -> comp
    | expr MATH_TYPE -> math

?func: symbol "." term

?term: arg | symbol

?arg: literal | "(" partial ")" | "{" block "}" | func

?partial: expr | operator

?literal: INTEGER -> integer
    | CHARACTER -> character
    | ESCAPED_CHARACTER -> escaped_character
    | HEX_CHARACTER -> escaped_character
    | ESCAPED_STRING -> string

symbol: CNAME
    | THIS_FUNC
    | "$" CNAME ("\\" CNAME)* -> import_
    | "$$" CNAME ("\\" CNAME)* -> c_import

%import common.ESCAPED_STRING
%import common.INT
%import common.CR
%import common.LF
%import common.CNAME
%ignore WS
WS: /[ \t\f]/+

INTEGER: INT
FLOATING: INT "." INT
ESCAPED_CHARACTER: /'\\[abfnrtv'\\]'/
HEX_CHARACTER: /'\\x[0-7][0-9a-fA-F]'/
CHARACTER: /'[^'\\]'/

MULTILINE_COMMENT: "#!" /((?!!#).|\n)*/ "!#" " "*
COMMENT: "#" /.*/
NEWLINE: (CR? LF)

THIS_FUNC: "@"
COMP_TYPE: "<=" | ">=" | "<" | ">" | "==" | "!="
MATH_TYPE: "+" | "-" | "*" | "/" | "%"
BOOL_TYPE: "->" | "||" | "&&"

_COM: COMMENT
    | MULTILINE_COMMENT

_SEP: NEWLINE
    | ";"

_LINE: _COM? _SEP
